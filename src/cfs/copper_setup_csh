#
#   Environment setup script for Copper. 
#
#   To use this script you should "cd" to the "cfs" directory where this
#   script is located and "source" it into your c-shell or from your
#   .login script.
#
#   Alternatively you may copy this script, remove the first if
#   statement and change the setting of the "here" variable to be
#   the location of your installed Copper hfs directory.
#
#   Note that this script modifies your search path by inserting the
#   Copper bin directory at the beginning. It may also sets the environment
#   variable LD_LIBRARY_PATH which is used to search for runtime libraries.
#
#   To run the script in quiet mode, specify the "-q" option on the
#   command line.
#
if ( ! -d copper  ||  ! -d dsolib  ||  ! -d bin ) then
    echo "You must cd to the Copper installation directory before"
    echo "sourcing this script. This allows the script to determine"
    echo "the installed location."
else
    if ( ! $?cwd ) set cwd=`/bin/pwd`
    set here="$cwd"
    setenv HFS	"$here"

    #
    #  The following are some handy shortcuts:
    #
    setenv H	"${HFS}"
    setenv HB	"${H}/bin"
    setenv HDSO	"${H}/dsolib"
    setenv HD	"${H}/demo"
    setenv HH	"${H}/copper"
    setenv HHC	"${HH}/config"
    setenv HT   "${H}/toolkit"
    setenv HSB  "${HH}/sbin"

    #
    #  The following is used as the generic /tmp path.  This is also
    # set on Windows to the temporary directory, so can be used for
    # system independent .hip files.
    #
    setenv TEMP /tmp

    #
    # Look for java.
    #
    if ( ! $?JAVA_HOME ) then
        # Check in PATH first
        # Use sh for this since CSH really really sucks
        set d=`sh -c 'which java 2>&1'`
        if ( "$status" == 0 ) then
            setenv JAVA_HOME `echo "$d" | sed 's/\/bin.*//'`
        else
            foreach dir (/usr/local /usr/local/java2 /usr/local/java /opt /usr/usr/java2 /usr/java)
                if ( -d "$dir" ) then
                    set d=`find $dir -maxdepth 3 -path "*/bin/java" -printf "%h\n" | head -1 | sed 's/\/bin//'`
                    if ( "$d" != "" ) then
                        setenv JAVA_HOME "$d"
                        break
                    endif
                endif
            end
        endif
    endif

    # We only need to set LD_LIBRARY_PATH if the environment also uses it. This
    # makes sure HDSO is always searched first.
    if ( $?LD_LIBRARY_PATH ) then
        setenv LD_LIBRARY_PATH "${HDSO}:${LD_LIBRARY_PATH}"
    endif

    # If the java binary exists but cannot be found, then add JAVA_HOME
    # to PATH.  Otherwise, only add HB and HSB to PATH.
    set java_path=`sh -c 'which java 2>&1'`
    set which_java=$status
    if ( $?JAVA_HOME && ${which_java} != 0 ) then
	setenv PATH "${HB}:${HSB}:${JAVA_HOME}/bin:$PATH"
    else
	setenv PATH "${HB}:${HSB}:$PATH"
    endif

    rehash

    setenv COPPER_MAJOR_RELEASE 0
    setenv COPPER_MINOR_RELEASE 0
    setenv COPPER_BUILD_VERSION 001
    setenv COPPER_VERSION "${COPPER_MAJOR_RELEASE}.${COPPER_MINOR_RELEASE}.${COPPER_BUILD_VERSION}"

    # Build machine related stuff
    setenv COPPER_BUILD_KERNEL "2.6.32-431.20.5.el6.x86_64"
    setenv COPPER_BUILD_PLATFORM "Red Hat Enterprise Linux Workstation release 6.5 (Santiago)"
    setenv COPPER_BUILD_COMPILER "4.8.2"

    # This only applies for linux systems
    setenv COPPER_BUILD_LIBC "glibc 2.12"

    if ( $?prompt && ( $#argv < 1 || "$1" != "-q" ) ) then
	echo "The Copper ${COPPER_VERSION} environment has been initialized."
    endif

    #
    # These environment variables are no longer supported.
    #
    setenv HIH	"${HOME}/copper${COPPER_MAJOR_RELEASE}.${COPPER_MINOR_RELEASE}"
    setenv HIS	"$HH"
endif
